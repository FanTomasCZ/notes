#| RouterMode:
#|  * WAN port is protected by firewall and enabled DHCP client
#|  * IP address 192.168.88.1/24 is set on LAN port
#| LAN Configuration:
#|     switch group: ether2 (master), ether3, ether4, ether5
#|     DHCP Server: enabled;
#|     DNS: enabled;
#| WAN (gateway) Configuration:
#|     gateway:  ether1 ;
#|     firewall:  enabled;
#|     NAT:   enabled;

:log info Starting_defconf_script_;
:global action;
#-------------------------------------------------------------------------------
# Apply configuration.
# these commands are executed after installation or configuration reset
#-------------------------------------------------------------------------------
:if ($action = "apply") do={
# wait for interfaces
:local count 0; 
:while ([/interface ethernet find] = "") do={ 
:if ($count = 30) do={
:log warning "DefConf: Unable to find ethernet interfaces";
/quit;
}
:delay 1s; :set count ($count +1); 
};

 /interface ethernet {
   set ether2 name=ether2-master;
   set ether3 master-port=ether2-master;
   set ether4 master-port=ether2-master;
   set ether5 master-port=ether2-master;
 }
  /ip address add address=192.168.88.1/24 interface=ether2-master comment="defconf";
   /ip pool add name="default-dhcp" ranges=192.168.88.10-192.168.88.254;
   /ip dhcp-server
     add name=defconf address-pool="default-dhcp" interface=ether2-master lease-time=10m disabled=no;
   /ip dhcp-server network
     add address=192.168.88.0/24 gateway=192.168.88.1 comment="defconf";
 /ip dns {
     set allow-remote-requests=yes
     static add name=router address=192.168.88.1
 }

   /ip dhcp-client add interface=ether1 disabled=no comment="defconf";
 /ip firewall nat add chain=srcnat out-interface=ether1 action=masquerade comment="defconf: masquerade"
 /ip firewall {
   filter add chain=input action=accept protocol=icmp comment="defconf: accept ICMP"
   filter add chain=input action=accept connection-state=established,related comment="defconf: accept establieshed,related"
   filter add chain=input action=drop in-interface=ether1 comment="defconf: drop all from WAN"
   filter add chain=forward action=fasttrack-connection connection-state=established,related comment="defconf: fasttrack"
   filter add chain=forward action=accept connection-state=established,related comment="defconf: accept established,related"
   filter add chain=forward action=drop connection-state=invalid comment="defconf: drop invalid"
   filter add chain=forward action=drop connection-state=new connection-nat-state=!dstnat in-interface=ether1 comment="defconf:  drop all from WAN not DSTNATed"
 }
 /ip neighbor discovery set [find name="ether1"] discover=no
 /tool mac-server disable [find];
 /tool mac-server mac-winbox disable [find];
 :foreach k in=[/interface find where !(slave=yes  || name~"ether1")] do={
   :local tmpName [/interface get $k name];
   /tool mac-server add interface=$tmpName disabled=no;
   /tool mac-server mac-winbox add interface=$tmpName disabled=no;
 }
}
#-------------------------------------------------------------------------------
# Revert configuration.
# these commands are executed if user requests to remove default configuration
#-------------------------------------------------------------------------------
:if ($action = "revert") do={
# remove wan port protection
 /ip firewall filter remove [find comment~"defconf"]
 /ip firewall nat remove [find comment~"defconf"]
 /tool mac-server remove [find interface!=all]
 /tool mac-server set [find] disabled=no
 /tool mac-server mac-winbox remove [find interface!=all]
 /tool mac-server mac-winbox set [find] disabled=no
 /ip neighbor discovery set [find ] discover=yes
   :local o [/ip dhcp-server network find comment="defconf"]
   :if ([:len $o] != 0) do={ /ip dhcp-server network remove $o }
   :local o [/ip dhcp-server find name="defconf" !disabled]
   :if ([:len $o] != 0) do={ /ip dhcp-server remove $o }
   /ip pool {
     :local o [find name="default-dhcp" ranges=192.168.88.10-192.168.88.254]
     :if ([:len $o] != 0) do={ remove $o }
   }
   :local o [/ip dhcp-client find comment="defconf"]
   :if ([:len $o] != 0) do={ /ip dhcp-client remove $o }
 /ip dns {
   set allow-remote-requests=no
   :local o [static find name=router address=192.168.88.1]
   :if ([:len $o] != 0) do={ static remove $o }
 }
 /ip address {
   :local o [find comment="defconf"]
   :if ([:len $o] != 0) do={ remove $o }
 }
 :foreach iface in=[/interface ethernet find] do={
   /interface ethernet set $iface name=[get $iface default-name]
   /interface ethernet set $iface master-port=none
 }
 /interface bridge port remove [find comment="defconf"]
 /interface bridge remove [find comment="defconf"]
}
:log info Defconf_script_finished;

